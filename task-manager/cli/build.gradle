import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'application'
    id 'com.gradleup.shadow' version '9.1.0'
    id 'com.bmuschko.docker-java-application' version '9.4.0'
}

group = "hu.botagergo"
version = "1.0-SNAPSHOT"

configurations {
    extraLibs
}

dependencies {
    implementation          project(':common:util')
    implementation          project(':common:property-lib')
    implementation          project(':common:cli')
    implementation          project(':common:core')
    implementation          project(':common:backend')
    implementation          project(':task-manager:backend')

    implementation          libs.commons.cli
    implementation          libs.guice
    implementation          libs.jackson.databind
    testImplementation      libs.jackson.databind
    implementation          libs.jackson.datatype.jsr310
    testImplementation      libs.jackson.datatype.jsr310
    implementation          libs.inamik.text.tables
    implementation          libs.jakarta.inject.api
    implementation          libs.jansi
    implementation          libs.jline
    runtimeOnly             libs.log4j.jul
    implementation          libs.openai.gpt3
    implementation          libs.snakeyaml
}

application {
    mainClass = 'cli_tools.task_manager.cli.TaskManagerCLI'
}

tasks.withType(Jar).configureEach {
    archiveBaseName = "task-manager-cli"
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    manifest {
        attributes "Multi-Release": true
    }
}

tasks.withType(ShadowJar).configureEach {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": "cli_tools.task_manager.cli.TaskManagerCLI"
    }
}

docker {
    registryCredentials {
        url = 'npipe:////.pipe/docker_engine'
    }
}

tasks.register("createDockerfile", Dockerfile) {
    dependsOn shadowJar
    def jarName = shadowJar.archiveFileName.get()
    from "eclipse-temurin:21-jre-alpine"
    copyFile("build/libs/$jarName", "/app/")
    entryPoint("java", "-jar", "/app/$jarName", "--daemon")
}

tasks.register("buildDockerImage", DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = project.projectDir
    setDockerFile(new File("build/docker/Dockerfile"))
    images.add("task-manager-daemon:latest")
}

tasks.register("createDockerContainer", DockerCreateContainer) {
    dependsOn buildDockerImage
    imageId = "task-manager-daemon:latest"
    hostConfig.portBindings = ['8224:8224']
}

tasks.register("startDockerContainer", DockerStartContainer) {
    dependsOn createDockerContainer
    containerId = createDockerContainer.getContainerId()
}

